/* ----------------------Preliminary Declarations Section--------------------*/

/* Import the class java_cup.runtime.*  */
package go.core;
import java_cup.runtime.*;

init with {: Semantic.parser = this;              :};
/* Parser code to change the way the parser reports errors (include
   line and column number of the error). */
parser code {:

    /* Change the method report_error so it will display the line and
       column of where the error occurred in the input as well as the
       reason for the error which is passed into the method in the
       String 'message'. */
    public void report_error(String message, Object info) {

        /* Create a StringBuilder called 'm' with the string 'Error' in it. */
        StringBuilder m = new StringBuilder("Error");

        /* Check if the information passed to the method is the same
           type as the type java_cup.runtime.Symbol. */
        if (info instanceof java_cup.runtime.Symbol) {
            /* Declare a java_cup.runtime.Symbol object 's' with the
               information in the object info that is being typecasted
               as a java_cup.runtime.Symbol object. */
            java_cup.runtime.Symbol s = ((java_cup.runtime.Symbol) info);

            /* Check if the line number in the input is greater or
               equal to zero. */
            if (s.left >= 0) {
                /* Add to the end of the StringBuilder error message
                   the line number of the error in the input. */
                m.append(" in line "+(s.left+1));
                /* Check if the column number in the input is greater
                   or equal to zero. */
                if (s.right >= 0)
                    /* Add to the end of the StringBuilder error message
                       the column number of the error in the input. */
                    m.append(", column "+(s.right+1));
            }
        }

        /* Add to the end of the StringBuilder error message created in
           this method the message that was passed into this method. */
        m.append(" : "+message);

        /* Print the contents of the StringBuilder 'm', which contains
           an error message, out on a line. */
        System.out.println(m);
        System.out.println(message);
        System.out.println(info);
    }

    /* Change the method report_fatal_error so when it reports a fatal
       error it will display the line and column number of where the
       fatal error occurred in the input as well as the reason for the
       fatal error which is passed into the method in the object
       'message' and then exit.*/
    public void report_fatal_error(String message, Object info) {
        report_error(message, info);
        System.exit(1);
    }
:};

init with {:
  StringBuilder res_code = new StringBuilder();
:};



/* ------------Declaration of Terminals and Non Terminals Section----------- */

/* Terminals (tokens returned by the scanner).

   Terminals that have no value are listed first and then terminals
   that do have an value, in this case an integer value, are listed on
   the next line down. */
terminal BREAK;
terminal DEFAULT;
terminal FUNC;
terminal INTERFACE;
terminal SELECT;
terminal CASE;
terminal DEFER;
terminal GO;
terminal MAP;
terminal STRUCT;
terminal CHAN;
terminal ELSE;
terminal GOTO;
terminal PACKAGE;
terminal SWITCH;
terminal CONST;
terminal FALLTHROUGH;
terminal IF;
terminal RANGE;
terminal TYPE;
terminal CONTINUE;
terminal FOR;
terminal IMPORT;
terminal RETURN;
terminal VAR;

/* language terminals  */
terminal BOOL; // primitive_type
terminal COMPLEX64, COMPLEX128; // complex types
terminal RUNE, BYTE, SHORT, UINTPTR, UINT8, UINT16, UINT32, UINT64, UINT, INT8, INT16, INT32, INT64, INT, LONG, CHAR; // integral_type
terminal FLOAT32, FLOAT64; // floating_point_type
terminal LBRACK, RBRACK; // array_type
terminal DOT; // qualified_name
terminal SEMICOLON, MULT, COMMA, LBRACKET, RBRACKET, LBRACE, RBRACE, EQ, LPAREN, RPAREN, COLON;
terminal PIPE, PIPEEQ, PIPEPIPE; // | ?
terminal PLUSPLUS; // postincrement_expression
terminal MINUSMINUS; // postdecrement_expression
terminal PLUS, MINUS, COMP, NOT, DIV, MOD;
terminal LSHIFT, RSHIFT, URSHIFT; // shift_expression
terminal LT, GT, LTEQ, GTEQ; // relational_expression
terminal CIRCU, CIRCUEQ;
terminal ARRLEFT;
terminal INFUNCEQ;
terminal ELLIPSIS;

terminal EQEQ, NOTEQ; // equality_expression
terminal AND; // and_expression
terminal ANDNOT;
terminal XOR; // exclusive_or_expression
terminal OR;  // inclusive_or_expression
terminal ANDAND; // conditional_and_expression
terminal OROR; // conditional_or_expression
terminal QUESTION; // conditional_expression
terminal MULTEQ, DIVEQ, MODEQ, PLUSEQ, MINUSEQ; // assignment_operator
terminal LSHIFTEQ, RSHIFTEQ, URSHIFTEQ; // assignment_operator
terminal ANDEQ, ANDNOTEQ, XOREQ, OREQ; // assignment_operator

terminal java.lang.Number INTEGER_LITERAL;
terminal java.lang.Float FLOATING_POINT_LITERAL;
terminal java.lang.Boolean BOOLEAN_LITERAL;
terminal java.lang.Character CHARACTER_LITERAL;
terminal java.lang.Character HEX_LITERAL;

terminal java.lang.String IMAGINARY_LITERAL;
terminal java.lang.String OCTAL_LITERAL;
terminal java.lang.String STRING_LITERAL;
terminal java.lang.String IDENTIFIER; // name
terminal java.lang.String QUALIFIED_IDENTIFIER;

// 19.2) The Syntactic Grammar
non terminal goal;
non terminal operand, operandName;
non terminal assignment, assignments_opt;
non terminal expression, unary_expression, primary_expression, expressions_opt, return_opt;
non terminal index, slice, selector, method_name, type_assertion, expression_list, expression_list_opt, conversion;

// 19.3) Lexical Structure
non terminal literal;
non terminal operator, unary_operator;
non terminal binary_operator;
non terminal relational_operator;
non terminal add_operator;
non terminal mult_operator;
non terminal assign_operator;

// 19.4) Types, Values, and Variables
non terminal type, type_name, type_literal, type_opt, primitive_type, numeric_type;
non terminal integral_type, floating_point_type;
non terminal slice_type, map_type, channel_type;
non terminal function_type;
non terminal class_type, interface_type;
non terminal struct_type, field_declaration, embedded_field, asterisk_opt, tag_opt, field_declarations;
non terminal array_type;

non terminal compilation_unit;

non terminal package_declaration_opt, package_declaration, package_name;
non terminal import_declaration;
non terminal func_declaration, func_body;
non terminal Statements, Statement;
non terminal top_level_decl;
non terminal untyped_params, untyped_param, typed_params, typed_param, params_opt;
non terminal args;
non terminal declaration;
non terminal const_decl, type_decl, type_decl_aux, type_spec, type_specs, alias_decl, type_def, var_decl, var_specs, var_spec, var_spec_aux, method_decl;
non terminal block;
non terminal EOF;
non terminal const_spec;
non terminal identifier_list;
non terminal top_import_decl;
non terminal const_spec_b;
non terminal if_stmt, else_stmt;
non terminal simple_stmt;
non terminal if_head;
non terminal simple_stmt_b;
non terminal for_stmt, for_clause, for_head;
non terminal inc_dec_stmt, inc_dec;
non terminal range_cl_head, range_cl,range_op;
non terminal label_stmt, label, label_opt;
non terminal continue_stmt;
non terminal send_stmt;
non terminal go_stmt, goto_stmt;
non terminal break_stmt;
non terminal fall_stmt;
non terminal defer_stmt;

/* precedência de operadores, os de baixo tem maior precedência */

precedence left LPAREN;
precedence left PIPEPIPE;
precedence left ANDAND;
precedence left EQEQ, NOTEQ, LT, GT, LTEQ, GTEQ;
precedence left PIPE, PLUS, MINUS, CIRCU;
precedence left MULT, DIV, MOD, LSHIFT, RSHIFT, AND, ANDNOT;
precedence left NOT, ARRLEFT;



  start with goal;
  
  goal ::= compilation_unit:c {: RESULT = c; :};

  compilation_unit ::=
  		package_declaration_opt top_import_decl
      {: RESULT = "cunitcode"; System.out.println("compilation_unit matched");  :};
      
  
  top_import_decl ::=  | import_declaration top_import_decl | top_level_decl top_import_decl;
  
  top_level_decl ::= declaration | func_declaration | method_decl;
  
  declaration ::=  const_decl | var_decl | type_decl;

  package_declaration_opt ::= | PACKAGE package_name;
  
  package_name ::= IDENTIFIER;
  	
  import_declaration ::= IMPORT STRING_LITERAL;

  func_declaration ::= FUNC IDENTIFIER LPAREN params_opt RPAREN type_opt func_body;

  func_body ::= block;
  
  block ::= LBRACE Statements return_opt RBRACE;
  
  Statements ::= | Statement Statements;
  
  Statement ::= declaration | defer_stmt | fall_stmt | goto_stmt | go_stmt | send_stmt | continue_stmt | break_stmt | label_stmt | expression | assignment | if_stmt | for_stmt;
  
  assignments_opt ::= | assignment assignments_opt;
  
  expressions_opt ::= | expressions_opt expression;
  
  return_opt ::= RETURN expression |  ;
    
  // Em go os tipos dos parametros devem ser explicitados em declarações de funcao, porem caso
  // tenhamos varios parametros seguidos do mesmo tipo, podemos omitir o tipo deixando apenas o 
  // ultimo parametro com o tipo explicito.
  
  params_opt ::=  | typed_params | untyped_params COMMA typed_param;
  
  untyped_params ::= untyped_param | untyped_params COMMA untyped_param;
  
  typed_params ::= typed_param | typed_params COMMA typed_param;
 
  untyped_param ::= IDENTIFIER;
  
  // O tipo do parametro pode ser uma classe definida pelo usuario, 
  //entao qualquer IDENTIFIER é sintaticamente válido como tipo.
 
  typed_param ::= IDENTIFIER type;
  
  type_opt ::= | type;
  
  
  
  /* Types */
  
  type ::= type_name | type_literal | LPAREN type RPAREN;
  
  type_literal ::= array_type | struct_type;
    
  type_name ::= QUALIFIED_IDENTIFIER | IDENTIFIER | primitive_type | numeric_type;
  
  array_type ::=
  	LBRACK expression RBRACK type;
  	
   /* Struct type */
  	
	  struct_type ::=
	  	STRUCT LBRACE field_declarations RBRACE;
	  	
	  field_declarations ::= field_declarations field_declaration tag_opt | ;
	  
	  field_declaration ::= 
	  	IDENTIFIER embedded_field | 
	  	QUALIFIED_IDENTIFIER | 
	  	//IDENTIFIER | 
	  	primitive_type | 
	  	numeric_type |
	  	MULT QUALIFIED_IDENTIFIER | 
	  	MULT IDENTIFIER | 
	  	MULT primitive_type | 
	  	MULT numeric_type;
	  	
  	  embedded_field ::= COMMA IDENTIFIER embedded_field type |;
	  	
	  tag_opt ::= | STRING_LITERAL;
	  
	  //embedded_field ::= MULT type_name | type_name;
	  
   /* Struct type */
  
  primitive_type ::= BOOL;
  
  numeric_type ::= floating_point_type | integral_type;
  	
  floating_point_type ::= FLOAT32 | FLOAT64;
 
  integral_type ::= COMPLEX64 | COMPLEX128 | 
	RUNE | BYTE | SHORT | UINTPTR | UINT8 | UINT16 | 
	UINT32 | UINT64 | UINT | INT8 | INT16 | INT32 | 
	INT64 | INT | LONG | CHAR;
	
  /* Types */
   
   
	
  literal ::= INTEGER_LITERAL | FLOATING_POINT_LITERAL | BOOLEAN_LITERAL | CHARACTER_LITERAL
   | HEX_LITERAL | IMAGINARY_LITERAL | OCTAL_LITERAL | STRING_LITERAL;
  	
  operator ::= binary_operator | relational_operator | add_operator | mult_operator;
  
  binary_operator ::= PIPEPIPE | ANDAND | relational_operator | add_operator | mult_operator;
  
  add_operator ::= PIPE | PLUS | MINUS | CIRCU;
  
  relational_operator ::= EQEQ | NOTEQ | LT | GT | LTEQ | GTEQ;
  
  unary_operator ::= PLUS | MINUS | NOT | CIRCU | MULT | AND | ARRLEFT;
  
  mult_operator ::= MULT | DIV | MOD | LSHIFT | RSHIFT | AND | ANDNOT;
  
  assign_operator ::= MULTEQ | DIVEQ | MODEQ | LSHIFTEQ | RSHIFTEQ | ANDEQ | ANDNOTEQ |
  	PIPEEQ | PLUSEQ | MINUSEQ | CIRCUEQ | EQ; 
   
  expression ::= unary_expression | expression binary_operator expression;
  
  unary_expression ::= primary_expression | unary_operator unary_expression;
  
  primary_expression ::= 
  
	operand |
	   
	conversion |
    
    method_name |
    
	primary_expression selector | 
    
	primary_expression index | 
    
	primary_expression slice |
  	
	primary_expression type_assertion |
  	
	primary_expression args;
  		
	
  	/* Algumas regras estão comentadas para evitar ambiguidade 
  	   pois é possível derivá-las de outra forma */
  	  	

	
  args ::= 	
  		LPAREN expression_list RPAREN |
  		LPAREN RPAREN;
	
	
  operand ::=   	
	  	literal |
	    IDENTIFIER | 
	    QUALIFIED_IDENTIFIER | 
	    LPAREN expression RPAREN ;
	    
	    
  conversion ::=		
  		//IDENTIFIER LPAREN expression RPAREN |
		primitive_type LPAREN expression RPAREN | 
		numeric_type LPAREN expression RPAREN ;
		
		
  selector ::= DOT IDENTIFIER;
  
  
  method_name ::= 	    
	    //IDENTIFIER DOT IDENTIFIER |
	    primitive_type DOT IDENTIFIER |
	    numeric_type DOT IDENTIFIER ;
	    
	    
  type_assertion ::= 
  		DOT LPAREN IDENTIFIER RPAREN | 
  		DOT LPAREN primitive_type RPAREN | 
  		DOT LPAREN numeric_type RPAREN;
  	
	
	
  /* Const Declaration */

  const_decl ::= CONST const_spec | CONST LPAREN const_spec_b RPAREN;

  const_spec ::= identifier_list type EQ expression_list | identifier_list EQ expression_list;
  
  const_spec_b ::= | identifier_list const_spec_b | identifier_list EQ expression_list const_spec_b;

  /* Const Declaration */
	

  /* IF Statement */
  
  if_stmt ::= 
  	IF if_head block |
  	IF if_head block else_stmt;
  	
  if_head ::= simple_stmt expression | expression;
  
  else_stmt ::= ELSE if_stmt | ELSE block;
  
  //TODO: adicionar inc dec stmt e short var decl
  simple_stmt ::= simple_stmt_b SEMICOLON;
  simple_stmt_b ::=  | expression | expression ARRLEFT expression | assignment | inc_dec_stmt;
  
  
  /* IF Statement */
  
	
  /* Variable Declaration */
  
   var_decl ::= VAR var_spec | VAR LPAREN var_specs RPAREN;
   
   var_specs ::= var_spec | var_spec var_specs ;
   
   var_spec ::= 
   		identifier_list var_spec_aux;
   		
   var_spec_aux ::=
   		 type |
   		 EQ expression_list |
   		 type EQ expression_list;
   		
  /* Variable Declaration */
  

  /* Type Declaration */
  
    type_decl ::= TYPE type_decl_aux;
    
    type_decl_aux ::= type_spec | LPAREN type_specs RPAREN;
    
    type_spec ::= alias_decl | type_def;
    
    type_specs ::= type_spec| type_spec type_specs;
    
    type_def ::= IDENTIFIER type;
    
    alias_decl ::= IDENTIFIER EQ type;
  
  /* Type Declaration */
	
	
	/* For */
	
	for_stmt ::= FOR for_head block;
	for_head ::= | expression | for_clause | range_cl;
	
	for_clause ::= simple_stmt_b SEMICOLON simple_stmt_b SEMICOLON simple_stmt_b;
	
	/* For */	
	
	/* Range Clause */
	
	range_cl ::= range_cl_head RANGE expression;
	
	range_cl_head ::= | expression_list range_op;
	range_op ::= EQ | DIVEQ;
	
	/* Range Clause */
	
	/* INC_DEC */
	
	inc_dec_stmt ::= expression inc_dec;
	inc_dec ::= PLUSPLUS | MINUSMINUS;
	
	/* INC_DEC */
	
	/* Label Statement */
	
	label_stmt ::= IDENTIFIER COLON Statement;
	label_opt ::= IDENTIFIER | SEMICOLON;
	/* Label Statement */
	
	/* Continue Statement */
	continue_stmt ::= CONTINUE label_opt;
	/* Continue Statement */
	
	/* Break Statement */
	break_stmt ::= BREAK label_opt;
	/* Break Statement */
	
	/* GOTO Statement */
	goto_stmt ::= GOTO IDENTIFIER;
	/* GOTO Statement */
	
	/* Send Statement */
	send_stmt ::= expression ARRLEFT expression;
	/* Send Statement */
	
	/* FALLTHROUGH Statement */
	fall_stmt ::= FALLTHROUGH;
	/* FALLTHROUGH Statement */
	
	/* DEFER Statement */
	defer_stmt ::= DEFER expression;
	/* DEFER Statement */
	
	go_stmt ::= GO expression;
	
  expression_list_opt ::= | expression_list;

  expression_list ::= expression | expression_list COMMA expression;
  
  identifier_list ::= IDENTIFIER | identifier_list COMMA IDENTIFIER;
 
  index ::= LBRACK expression RBRACK;

  slice ::= LBRACK expression COLON expression RBRACK | 
  	LBRACK expression COLON expression COLON expression RBRACK;
  
  assignment ::= expression_list assign_operator expression_list;
   
